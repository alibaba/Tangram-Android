/*
 * MIT License
 *
 * Copyright (c) 2017 Alibaba Group
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

apply plugin: 'com.android.library'

ext {
    bintrayRepo = 'Tangram'
    bintrayName = 'Tangram-Android'

    publishedGroupId = GROUP
    libraryName = ARTIFACT
    artifact = ARTIFACT

    libraryDescription = 'Tangram is a modular UI solution for building native page dynamically including Tangram for Android, Tangram for iOS and even backend CMS. This project provides the sdk on Android.'

    siteUrl = 'https://github.com/alibaba/Tangram-Android'
    gitUrl = 'https://github.com/alibaba/Tangram-Android.git'

    libraryVersion = VERSION_NAME

    developerId = 'longerian'
    developerName = 'longerian'
    developerEmail = 'xlhongultimate@gmail.com'

    licenseName = 'MIT'
    licenseUrl = 'https://opensource.org/licenses/MIT'
    allLicenses = ["MIT"]
}

def VERSION = System.properties['version'] ?: '10'
def VERSION_NAME = System.properties['versionName'] ?: '0.1.0'

android {
    compileSdkVersion Integer.parseInt(System.properties['compileSdkVersion'] ?: '22')
    buildToolsVersion System.properties['buildToolsVersion'] ?: '21.0.2'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion Integer.parseInt(System.properties['targetSdkVersion'] ?: '19')
        versionCode Integer.parseInt(VERSION)
        versionName VERSION_NAME
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

configurations.all {
    // check for updates every build
    resolutionStrategy.cacheChangingModulesFor 60, 'seconds'
}


dependencies {

    compile('com.alibaba.android:vlayout:1.2.6@aar') {
        changing = true
    }

    compile 'com.android.support:recyclerview-v7:23.1.1@aar'
    compile 'com.android.support:support-v4:23.1.1@aar'
    compile 'com.android.support:support-annotations:23.1.1'
    compile 'com.alibaba.android:ultraviewpager:1.0.6.3@aar'
    compile('me.everything:overscroll-decor-android:1.0.1@aar') {
        transitive true
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }
    compile('com.alibaba.android:virtualview:1.0.1@aar') {
        transitive true
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.tmall.android', module: 'tmallandroid_mui'
        exclude group: 'com.google.android', module: 'support-v7-recyclerview'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'support-annotations'
    }

}

File deployConfig = rootProject.file('jcenterInstall.gradle')
if (deployConfig.exists()) {
    apply from: rootProject.file('jcenterInstall.gradle')
}

deployConfig = rootProject.file('jcenterDeploy.gradle')
if (deployConfig.exists()) {
    apply from: rootProject.file('jcenterDeploy.gradle')
}